dnl Process this file with autoconf to produce a configure script.
AC_INIT(openCryptoki, 2.3.0, danjones@us.ibm.com,)

dnl Backup CFLAGS so that once the auto tools set it to "-g -O2",
dnl (which we want to avoid), we can restore them to whatever the
dnl user might have wanted below.
cmdline_CFLAGS="$CFLAGS"

# Compute $target
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE([foreign 1.6])

dnl Checks for programs.

dnl Checks for libraries.

dnl Checks for header files.
AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h strings.h sys/file.h syslog.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM
dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_MEMCMP
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([getcwd])

# Used in the init scripts
AC_SUBST(ID, /usr/bin/id)
AC_SUBST(USERMOD, /usr/sbin/usermod)
AC_SUBST(GROUPADD, /usr/sbin/groupadd)

dnl i.e., /usr/local or /usr
if test "x$prefix" = xNONE; then
  prefix=$ac_default_prefix
fi
if test "x$exec_prefix" = xNONE; then
  exec_prefix='${prefix}'
fi

CFLAGS="$cmdline_CFLAGS"

# Arch specific stuff
case $target in
	*64*)
		lib="lib64" ;;
	*)
		lib="lib" ;;
esac

if test "x$prefix" == x/usr; then
        AC_SUBST(CONFIG_PATH, /var/lib/opencryptoki)
	AC_SUBST(CONFIG_PATH_ETC, /etc)
        AC_SUBST(SBIN_PATH, /usr/sbin)
        AC_SUBST(DB_PATH, /var/lib/opencryptoki)
else
        AC_SUBST(CONFIG_PATH, $prefix/var/lib/opencryptoki)
	AC_SUBST(CONFIG_PATH_ETC, $prefix/etc)
        AC_SUBST(SBIN_PATH, $prefix/sbin)
        AC_SUBST(DB_PATH, $prefix/var/lib/opencryptoki)
fi

AC_SUBST(LIB_PATH, $prefix/$lib/opencryptoki)
AC_SUBST(STDLL_PATH, $LIB_PATH/stdll)
AC_SUBST(CONFIG_FILE, pk_config_data)
AC_SUBST(METHOD_PATH, $prefix/sbin)
AC_SUBST(HEADER_PATH, $prefix/include)

# Debugging support
AC_ARG_ENABLE(debug, [  --enable-debug          turn on all openCryptoki debugging flags],
                     [ enable_debug="yes"],)

if test "x$enable_debug" = xyes; then
	CFLAGS="$CFLAGS -g -O0 -DDEBUG -DDEBUGON"
	AC_MSG_RESULT([*** Enabling debugging at user request ***])
else
	CFLAGS="$CFLAGS -O2"
fi

# Support for OpenSSL path specification
AC_ARG_WITH(openssl,
        [  --with-openssl[[=DIR]]    build with OpenSSL support [[/usr/local/ssl]]],
        [openssl_prefix=$withval], 
        [openssl_prefix=]
)
if test "x$openssl_prefix" != x; then
	AC_MSG_RESULT([*** Using OpenSSL directory $openssl_prefix ***])
	LDFLAGS="-L$openssl_prefix/lib $LDFLAGS"
	CFLAGS="-I$openssl_prefix/include $CFLAGS"
fi

# Check if building daemon
AC_ARG_ENABLE(daemon, [  --disable-daemon        don't build pkcsslotd [default=enabled]],
              [AM_CONDITIONAL(DAEMON, false)],
              [AM_CONDITIONAL(DAEMON, true)])

# The software token is enabled by default on non-s390 platforms
AC_ARG_ENABLE(swtok,
              [  --disable-swtok         don't build the software token [default=enabled (except s390)]],
              [AM_CONDITIONAL(DEFAULT_DLL, false)],
              [enable_swtok="yes"])

if test "x$enable_swtok" = xyes; then
        AC_CHECK_LIB(crypto, AES_encrypt,
             [AM_CONDITIONAL(DEFAULT_DLL, true)],
             [AM_CONDITIONAL(DEFAULT_DLL, false)])
else
        AC_MSG_RESULT([*** Disabling the default software token at user request ***])
fi

# TPM support for the TPM token
AC_ARG_ENABLE(tpmtok,
              [  --enable-tpmtok         build the TPM token [default=disabled]],
              [enable_tpmtok="yes"],
              [enable_tpmtok="no"])

if test "x$enable_tpmtok" = xyes; then
	AC_CHECK_HEADER(tss/platform.h,
	AC_CHECK_LIB(crypto, AES_encrypt,
			AC_CHECK_LIB(tspi, Tspi_Context_Create,
				     [AM_CONDITIONAL(TPM, true)],
                                     [AM_CONDITIONAL(TPM, false)], 
                                     -lcrypto),
			[AM_CONDITIONAL(TPM, false)]),
		[AM_CONDITIONAL(TPM, false)])
else
	AM_CONDITIONAL(TPM, false)
fi

CFLAGS="$CFLAGS -DPKCS64 \
	-DCONFIG_PATH=\\\"$CONFIG_PATH\\\" \
	-DSBIN_PATH=\\\"$SBIN_PATH\\\" \
	-DLIB_PATH=\\\"$LIB_PATH\\\" \
	-D_XOPEN_SOURCE=500 -Wall -Wno-unused"

# At this point, CFLAGS is set to something sensible
AC_PROG_CC

AC_OUTPUT([Makefile usr/Makefile \
          usr/include/Makefile \
          usr/include/pkcs11/Makefile \
          usr/lib/Makefile \
          usr/lib/pkcs11/Makefile \
          usr/lib/pkcs11/api/Makefile \
          usr/lib/pkcs11/api/shrd_mem.c \
          usr/sbin/Makefile \
          usr/sbin/pkcsslotd/Makefile \
          usr/sbin/pkcs11_startup/Makefile \
          usr/sbin/pkcs11_startup/pkcs11_startup \
          usr/sbin/pkcs_slot/Makefile \
          usr/sbin/pkcs_slot/pkcs_slot \
          usr/sbin/pkcsconf/Makefile \
          usr/lib/pkcs11/soft_stdll/Makefile \
          usr/lib/pkcs11/soft_stdll/tok_struct.h \
          usr/lib/pkcs11/tpm_stdll/Makefile \
          usr/lib/pkcs11/tpm_stdll/tok_struct.h \
          misc/Makefile \
          misc/pkcsslotd]) 

echo
echo "CLFAGS=$CFLAGS"

